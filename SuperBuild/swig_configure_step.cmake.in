# Set the important environmental variables that auto-conf uses to
# configure, mostly these are passed from the CMake
# configuration. Others are just explicitly passed from the
# enviroment. If there is a problem with them in the future they can
# be addressed in CMake with the same structure.

cmake_policy(VERSION 3.10.0)

set(ENV{CC} "@CMAKE_C_COMPILER_LAUNCHER@ @CMAKE_C_COMPILER@")
set(ENV{CFLAGS} "@REQUIRED_C_FLAGS@ @CMAKE_C_FLAGS@ @CMAKE_C_FLAGS_RELEASE@")
set(ENV{LDFLAGS} "@CMAKE_LINKER_FLAGS@ @CMAKE_LINKER_FLAGS_RELEASE@")

set(ENV{LIBS} "$ENV{LIBS}")

set(ENV{CPP} "$ENV{CPP}")
set(ENV{CPPFLAGS} "@REQUIRED_C_FLAGS@ $ENV{CPPFLAGS}")

set(ENV{CXX} "@CMAKE_CXX_COMPILER_LAUNCHER@ @CMAKE_CXX_COMPILER@")
set(ENV{CXXFLAGS} "@REQUIRED_CXX_FLAGS@ @CMAKE_CXX_FLAGS@ @CMAKE_CXX_FLAGS_RELEASE@")

set(ENV{YACC} "@BISON_EXECUTABLE@" )
set(ENV{YFLAGS} "@BISON_FLAGS@" )

if(@USE_SWIG_FROM_GIT@)
  execute_process(
    COMMAND sh @swig_source_dir@/autogen.sh
    WORKING_DIRECTORY @swig_source_dir@
    )
endif()

execute_process(
  COMMAND sh @swig_source_dir@/configure
    --prefix=@swig_install_dir@
    --with-pcre-prefix=@pcre_install_dir@
    --without-octave
    --with-python=@PYTHON_EXECUTABLE@
  WORKING_DIRECTORY @swig_binary_dir@
)
